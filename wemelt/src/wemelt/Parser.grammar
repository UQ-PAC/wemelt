%package "wemelt";
%class "Parser";

%terminals VAR;
%terminals ARRAY;

//%terminals RETURN, BREAK, CONTINUE;
%terminals IF, ELSE, WHILE, DO;

%terminals ID, REG_ID;
%terminals NUM;
%terminals LOW, HIGH, TRUE, FALSE;

%terminals LPAREN, RPAREN, LBRACK, RBRACK, LBRACE, RBRACE;
%terminals POSTFIX;
%terminals PREFIX,  BANG, TILDE;
%terminals STAR, DIV, MOD;
%terminals PLUS, MINUS;
%terminals SHL, SHR, ASHR;
%terminals LT, LE, GE, GT;
%terminals EQ, NEQ;
%terminals AMP;
%terminals CARET;
%terminals PIPE;
%terminals AND;
%terminals OR;
%terminals COLON;
%terminals ASG;
%terminals COMMA, SEMICOLON;

%terminals CAS;
%terminals FENCE, CFENCE;
%terminals INVARIANT, GAMMA, P_0, GAMMA_0, STABLE;
%terminals LPRED, MODE;
%terminals NORW, NOW, RW;
%terminals MAPSTO;

%nonassoc IF;
%nonassoc ELSE;
%nonassoc LPAREN, RPAREN;
%right PREFIX;
%left  STAR, DIV, MOD;
%left  PLUS, MINUS;
%left  SHL, SHR, ASHR;
%left  LT, LE, GE, GT;
%left  EQ, NEQ;
%left  AMP;
%left  CARET;
%left  PIPE;
%left  AND;
%left  OR;
%right COLON;
%right ASG;
%left  COMMA;

%typeof ID      = "String";
%typeof REG_ID  = "String";

%typeof NUM     = "Integer";

%typeof id_reg_id = "Id";
%typeof stable = "Id";

%typeof expr    = "Expression";

%typeof local   = "Statement";
%typeof local1  = "Statement";
%typeof local2  = "Statement";

%typeof definition = "Definition";
%typeof lpred = "Expression";
%typeof mode = "Mode";
%typeof unit = "Global";
%typeof gamma_mapping = "GammaMapping";
%typeof security = "Security";
%typeof lpredindex = "Expression";

%goal unit;

unit = defs.v locals.s {: return new Global(v, s); :}
     | defs.v GAMMA_0 COLON gamma_mappings.f locals.s {: return new Global(v, f, s); :}
     | defs.v P_0 COLON exprs.e locals.s {: return new Global(v, e, s); :}
     | defs.v P_0 COLON exprs.e GAMMA_0 COLON gamma_mappings.f locals.s {: return new Global(v, e, f, s); :};

exprs = expr @ ASG | exprs COMMA expr;


local
    = SEMICOLON {: return Block.empty(); :}
    | local1
    ;

local2
	=  local1
    ;

locals  = local *;
locals2 = local2 *;

id_reg_id = ID.n {: return new Id(n); :}
          | REG_ID.n {: return new Id(n); :}
          ;

stable = id_reg_id.i {: return i; :}
         | id_reg_id.i LBRACK STAR RBRACK {: return i; :}
         | id_reg_id.i LBRACK NUM.n RBRACK {: return new Id(i.name() + "[" + n + "]"); :}
         ;

stables = stable | stables COMMA stable;

definition
    =     VAR REG_ID.n COLON lpred.l                 {: return new VarDef(n, l); :}
          | VAR REG_ID.n                        {: return new VarDef(n); :}
          | VAR ID.n COLON lpred.l mode.m              {:  return new VarDef(n, l, m); :}
          | VAR ID.n COLON mode.m                      {: return new VarDef(n, m); :}
          | ARRAY ID.n LBRACK NUM.s RBRACK COLON lpred.l mode.m {: return new ArrayDef(n, s, l, m); :}
          | ARRAY ID.n LBRACK NUM.s RBRACK COLON lpredindexes.l mode.m {: return new ArrayDef(n, s, l, m); :}
          | ARRAY ID.n LBRACK NUM.s RBRACK COLON mode.m {: return new ArrayDef(n, s, m); :}
          | ARRAY REG_ID.n LBRACK NUM.s RBRACK COLON lpred.l {: return new ArrayDef(n, s, l); :}
          | ARRAY REG_ID.n LBRACK NUM.s RBRACK COLON lpredindexes.l {: return new ArrayDef(n, s, l); :}
          | ARRAY REG_ID.n LBRACK NUM.s RBRACK  {: return new ArrayDef(n, s); :}
          ;

defs = definition *;

local1
    = LBRACE locals2.ss RBRACE          {: return new Block(ss); :}
    //| expr.a   SEMICOLON                {: return new Atomic(a); :}
    | id_reg_id.n ASG    expr.b SEMICOLON              {: return new Assignment(n, b); :}
    | id_reg_id.n ASG CAS LPAREN id_reg_id.x COMMA expr.y COMMA expr.z RPAREN SEMICOLON {: return new CompareAndSwap(n, x, y, z); :}
    | id_reg_id.n LBRACK expr.i RBRACK ASG    expr.b SEMICOLON        {: return new ArrayAssignment(n, i, b); :}
    //| RETURN   SEMICOLON                {: return Return.none(); :}
    //| RETURN expr.a SEMICOLON           {: return new Return(a); :}
    //| BREAK    SEMICOLON                {: return Break.self();  :}
    //| CONTINUE SEMICOLON                {: return Continue.self(); :}
    | FENCE SEMICOLON                   {: return Fence.self();  :}
    | CFENCE SEMICOLON                   {: return ControlFence.self();  :}
    | IF LPAREN expr.a RPAREN local.b   {: return new If(a, b);  :}
    | IF LPAREN expr.a RPAREN local.b ELSE local.c     {: return new If(a, b, c);   :}
    | WHILE LPAREN expr.a RPAREN INVARIANT COLON exprs.i GAMMA COLON gamma_mappings.g local.b      {: return new While(a, i, g, b);   :}
    | WHILE LPAREN expr.a RPAREN INVARIANT COLON exprs.i GAMMA COLON gamma_mappings.g STABLE COLON stables.n local.b      {: return new While(a, i, g, n, b);   :}
    | DO INVARIANT COLON exprs.i GAMMA COLON gamma_mappings.g local.b WHILE LPAREN expr.a RPAREN    {: return new DoWhile(a, i, g, b);   :}
    | DO INVARIANT COLON exprs.i GAMMA COLON gamma_mappings.g STABLE COLON stables.n local.b WHILE LPAREN expr.a RPAREN     {: return new DoWhile(a, i, g, n, b);   :}
    | error SEMICOLON {: return Malformed.self(); :}
    ;

expr
    = PREFIX | POSTFIX /* unused */
    | id_reg_id.n                              {: return n;  :}
    | id_reg_id.n LBRACK expr.i RBRACK       {: return new Access(n, i); :}
    | NUM.n                             {: return new Lit(n); :}
    | PLUS  expr.a @ PREFIX             {: return new PreOp("+", a);    :}
    | MINUS expr.a @ PREFIX             {: return new PreOp("-", a);    :}
    | BANG  expr.a @ PREFIX             {: return new PreOp("!", a);    :}
    | TILDE expr.a @ PREFIX             {: return new PreOp("~", a);    :}
    | expr.a STAR   expr.b              {: return new BinOp("*", a, b); :}
    | expr.a DIV    expr.b              {: return new BinOp("/", a, b); :}
    | expr.a MOD    expr.b              {: return new BinOp("%", a, b); :}
    | expr.a PLUS   expr.b              {: return new BinOp("+", a, b); :}
    | expr.a MINUS  expr.b              {: return new BinOp("-", a, b); :}
    | expr.a SHL    expr.b              {: return new BinOp("<<",a, b); :}
    | expr.a SHR    expr.b              {: return new BinOp(">>",a, b); :}
    | expr.a ASHR    expr.b             {: return new BinOp(">>>",a, b); :}
    | expr.a LT     expr.b              {: return new BinOp("<", a, b); :}
    | expr.a LE     expr.b              {: return new BinOp("<=",a, b); :}
    | expr.a GE     expr.b              {: return new BinOp(">=",a, b); :}
    | expr.a GT     expr.b              {: return new BinOp(">", a, b); :}
    | expr.a EQ     expr.b              {: return new BinOp("==",a, b); :}
    | expr.a NEQ    expr.b              {: return new BinOp("!=",a, b); :}
    | expr.a AMP    expr.b              {: return new BinOp("&", a, b); :}
    | expr.a CARET  expr.b              {: return new BinOp("^", a, b); :}
    | expr.a PIPE   expr.b              {: return new BinOp("|", a, b); :}
    | expr.a AND    expr.b              {: return new BinOp("&&",a, b); :}
    | expr.a OR     expr.b              {: return new BinOp("||",a, b); :}
    | LPAREN expr.e RPAREN              {: return (Symbol) e;           :}
    | TRUE                              {: return new Const("True");  :}
    | FALSE                             {: return new Const("False");  :}
    ;

// for _L(x): pred construction to set predicate for variable security level
lpred
    = LPRED COLON expr.a {: return (Symbol) a; :}
    ;

lpredindex = LPRED LBRACK NUM.n RBRACK COLON expr.a {: return (Symbol) a; :};

lpredindexes = lpredindex | lpredindexes lpredindex;

// for _Mode(x): NoRW etc. construction to set variable mode
mode
    = MODE COLON NORW {: return NoRW.instance(); :}
    | MODE COLON NOW {: return NoW.instance(); :}
    | MODE COLON RW {: return RW.instance(); :}
    ;

gamma_mappings
    = gamma_mapping | gamma_mappings COMMA gamma_mapping;

gamma_mapping
    = id_reg_id.n MAPSTO security.s {: return new GammaMapping(n, s); :}
    | id_reg_id.n LBRACK NUM.i RBRACK MAPSTO security.s {: return new GammaMapping(n, i, s); :}
    | id_reg_id.n LBRACK STAR RBRACK MAPSTO security.s {: return new GammaMapping(n, s); :}
    ;

security
    = LOW {: return Low.instance(); :}
    | HIGH {: return High.instance(); :}
    ;